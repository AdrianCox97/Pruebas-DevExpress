CREATE DATABASE MIBITACORA;

GO

USE MIBITACORA;

GO

CREATE TABLE BITACORA
(
	CLAVE INT IDENTITY(1, 1),
	PK VARCHAR(1500),
	ACCION CHAR(1),
	TABLA VARCHAR(100),
	CAMPO VARCHAR(100),
	VALORORIGINAL VARCHAR(1500),
	VALORNUEVO VARCHAR(1500),
	FECHA DATETIME2,
	IDUSUARIO VARCHAR(10)
);
go
alter table dbo.bitacora alter column  IDUsuario varchar(75) not null

GO

alter TRIGGER TR_BITACORA
ON CLIENTE
FOR INSERT, UPDATE, DELETE
AS
DECLARE
    @COLUMNA INT,
    @COLUMNA_MAX INT,
    @CAMPO VARCHAR(150),
    @TABLA VARCHAR(150),
    @PKCOL VARCHAR(1000),
    @FECHA DATETIME2,
    @ACCION char(1) ,	
    @PKSELECT varchar(1000),
	@IDUSUARIO varchar(10),
	@SQL VARCHAR(2000)

	--REMUEVE EL NÚMERO DE COLUMNAS AFECTADAS
	SET NOCOUNT ON

	--SE ASIGNA EL NOMBRE DE LA TABLA
	SELECT @TABLA = 'CLIENTE'

	-- Obtenemos la lista de columnas de los cursores
	SELECT * INTO #ins FROM inserted
	SELECT * INTO #del FROM deleted

	--SE LE ASIGNA EL ID DEL USUARIO QUE REALIZÓ EL CAMBIO
	SELECT @IDUSUARIO = IDUsuario from #ins;
	
	--SE OBTIENE LA FECHA Y HORA ACTUAL
	SELECT @FECHA = TODATETIMEOFFSET(CONVERT(DATETIME2, GETDATE()), '-06:00')

	--SE OBTIENE EL NOMBRE DE LA COLUMNA QUE CONTIENE LA PK
	SELECT @PKCOL = COALESCE(@PKCOL + ' and', ' on') +
					' I.' + IS_CU.COLUMN_NAME + ' = D.' + IS_CU.COLUMN_NAME
					FROM INFORMATION_SCHEMA.TABLE_CONSTRAINTS IS_C
					JOIN INFORMATION_SCHEMA.KEY_COLUMN_USAGE IS_CU
					ON IS_CU.TABLE_NAME = IS_C.TABLE_NAME
					AND IS_CU.CONSTRAINT_NAME = IS_C.CONSTRAINT_NAME
					WHERE IS_C.TABLE_NAME = @TABLA AND IS_C.CONSTRAINT_TYPE = 'PRIMARY KEY'
	
	--SE OBTIENE LA PK Y LAS COLUMNAS PARA LA INSERCIÓN DE DATOS EN LA TABLA DE BITÁCORA
	SELECT @PKSELECT = 	COALESCE(@PKSELECT + '+', '') + 
						'''<' + 
						COLUMN_NAME + 
						'='' + CONVERT(VARCHAR(100),COALESCE(I.' + 
						COLUMN_NAME +',D.' + 
						COLUMN_NAME + '))+''>''' 
						FROM INFORMATION_SCHEMA.TABLE_CONSTRAINTS IS_C
						JOIN INFORMATION_SCHEMA.KEY_COLUMN_USAGE IS_CU
						ON IS_CU.TABLE_NAME = IS_C.TABLE_NAME
						AND IS_CU.CONSTRAINT_NAME = IS_C.CONSTRAINT_NAME
						WHERE IS_C.TABLE_NAME = @TABLA
						AND CONSTRAINT_TYPE = 'PRIMARY KEY'

	--SE DETERMINA LA ACCIÓN QUE ELUSUARIO ESTÁ REALIZANDO
	if exists (SELECT * FROM inserted) 
		if exists (SELECT * FROM deleted) --SI ES UN UPDATE
			SELECT @ACCION = 'U'
		else                              --SI ES UN INSERT
			SELECT @ACCION = 'I'
	else                                  --SI ES UN DELETE
		SELECT @ACCION = 'D'

	--COMENTARIO
	SELECT @COLUMNA = 0;
	SELECT @COLUMNA_MAX = MAX(ORDINAL_POSITION)
						  FROM INFORMATION_SCHEMA.COLUMNS IS_C
						  WHERE IS_C.TABLE_NAME = @TABLA

	--SE INSERTA 1 REGISTRO POR CADA CAMPO AFECTADO
	WHILE @COLUMNA < @COLUMNA_MAX
	BEGIN
		SELECT @COLUMNA = MIN(ORDINAL_POSITION) 
		FROM INFORMATION_SCHEMA.COLUMNS 
		WHERE TABLE_NAME = @TABLA and ORDINAL_POSITION > @COLUMNA

			BEGIN
				SELECT @CAMPO = COLUMN_NAME 
				FROM INFORMATION_SCHEMA.COLUMNS 
				WHERE TABLE_NAME = @TABLA and ORDINAL_POSITION = @COLUMNA

				--SELECT @sql = 'INSERT INTO BITACORA (PK, ACCION, TABLA, CAMPO, VALORORIGINAL, VALORNUEVO, FECHA)'
				SELECT @sql = 'INSERT INTO BITACORA (ACCION, TABLA, PK, CAMPO, VALORORIGINAL, VALORNUEVO, FECHA, IDUSUARIO)'
				SELECT @sql = @sql + 	' SELECT ''' + @ACCION + ''''
				SELECT @sql = @sql + 	',''' + @TABLA + ''''
				SELECT @sql = @sql + 	',' + @PKSELECT
				SELECT @sql = @sql + 	',''' + @CAMPO + ''''
				SELECT @sql = @sql + 	',convert(varchar(1000),D.' + @CAMPO + ')'
				SELECT @sql = @sql + 	',convert(varchar(1000),I.' + @CAMPO + ')'
				SELECT @sql = @sql + 	',''' + CONVERT(VARCHAR, @FECHA) + ''''
				SELECT @sql = @sql + 	',''' + @IDUSUARIO+ ''''
				SELECT @sql = @sql + 	' from #ins I full outer join #del D '
				SELECT @sql = @sql + 	@PKCOL
				SELECT @sql = @sql + 	' where I.' + @CAMPO + ' <> D.' + @CAMPO 
				SELECT @sql = @sql + 	' or (I.' + @CAMPO + ' is null and  D.' + @CAMPO + ' is not null)' 
				SELECT @sql = @sql + 	' or (I.' + @CAMPO + ' is not null and D.' + @CAMPO + ' is null)' 
				exec (@sql)
			END
	END
GO

--INSERT INTO Cliente (Nombres, Apellidos, NumeroDocumento, TipoDocumento, IDUsuario)
--VALUES ('JESUS', 'TORRES', 6818, 'DNI', '15090526');

UPDATE CLIENTE SET Nombres = 'MARIA', Apellidos = 'ESTEFANIA' WHERE IdCliente = 10;

--DELETE FROM CLIENTE WHERE IdCliente = 4;

SELECT * FROM CLIENTE;
SELECT * FROM BITACORA;

TRUNCATE TABLE CLIENTE;
TRUNCATE TABLE BITACORA;